import glob

####
# import the environment
####
Import('env variant_dir TOOLPATH')
env = env.Clone()
env.Append(CPPDEFINES=["APPLICATION_XERCES"],CPPPATH=["$COREDIR/Wizard","#/WizardItems"],LIBS=["Wizard","Utilities","PlugInUtilities"])

####
# build sources
####
srcs = map(lambda x,bd=variant_dir: '%s/%s' % (bd,x), glob.glob("*.cpp"))
objs = env.SharedObject(srcs)
wizard_extras = []
if env["OS"] != "windows":
   wizard_static_lib = env.File("%s/libWizard.a" % env["LIBDIR"])
   utilities_static_lib = env.File("%s/libUtilities.a" % env["LIBDIR"])
   class SharedObjectProxy:
      def __init__(self):
         self.shared = 1
   wizard_static_lib.attributes = SharedObjectProxy()
   utilities_static_lib.attributes = SharedObjectProxy()
   wizard_extras = [wizard_static_lib,utilities_static_lib]

####
# build the plug-in library and set up an alias to ease building it later
####
lib = env.SharedLibrary('%s/WizardExecutor' % variant_dir,objs+wizard_extras)
libInstall = env.Install(env["PLUGINDIR"], lib)
env.Alias('WizardExecutor', libInstall)

####
# return the plug-in library
####
Return("libInstall")
