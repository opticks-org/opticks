import glob

####
# import the environment
####
Import('env build_dir TOOLPATH')
env = env.Clone()
env.Append(CPPDEFINES=["APPLICATION_XERCES"])
env.Tool("ArcProxyLib",toolpath=[TOOLPATH])
env.Tool("shapelib",toolpath=[TOOLPATH])
env.Tool("gdal", toolpath=[TOOLPATH])
env.Tool("proj4", toolpath=[TOOLPATH])
if env["OS"] == "windows":
   if env["MODE"] == "debug":
      env.AppendUnique(LIBS=["QtNetworkd4"])
   else:
      env.AppendUnique(LIBS=["QtNetwork4"])

####
# build sources
####
srcs = map(lambda x,bd=build_dir: '%s/%s' % (bd,x), glob.glob("*.cpp"))
srcs.append(env.Qrc("%s/GeographicFeatures.qrc" % build_dir))
objs = env.SharedObject(srcs)

####
# build the plug-in library and set up an alias to wase building it later
####
lib = env.SharedLibrary('%s/GeographicFeatures' % build_dir,objs)
libInstall = env.Install(env["PLUGINDIR"], lib)
env.Alias('GeographicFeatures', libInstall)

####
# return the plug-in library
####
Return("libInstall")
